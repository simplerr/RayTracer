#version 450

/*
	http://www.scratchapixel.com/lessons/3d-basic-rendering/minimal-ray-tracer-rendering-simple-shapes/ray-sphere-intersection
	https://github.com/LWJGL/lwjgl3-wiki/wiki/2.6.2.-Ray-tracing-with-OpenGL-Compute-Shaders-%28Part-II%29
*/

uniform float time;

layout(binding = 0, rgba8) uniform image2D framebuffer;

layout (local_size_x = 16, local_size_y = 16) in;

uniform vec3 eye;
uniform vec3 ray00;
uniform vec3 ray01;
uniform vec3 ray10;
uniform vec3 ray11;

struct box {
	vec3 min;
	vec3 max;
};

struct sphere {
	vec3 center;
	float radius;
};

#define NUM_BOXES 2
#define NUM_SPHERES 2
#define MAX_SCENE_BOUNDS 100.0

const box boxes[] = {
	/* The ground */
	{vec3(-5.0, -0.1, -5.0), vec3(5.0, 0.0, 5.0)},
	/* Box in the middle */
	{vec3(-0.5, 0.0, -0.5), vec3(0.5, 1.0, 0.5)}
};

const sphere spheres[] = {
	{vec3(1, 3, 1), 1},
	{vec3(3, 1, 1), 2}
};

struct hitinfo {
	vec2 lambda;
	int bi;
};

vec2 intersectBox(vec3 origin, vec3 dir, const box b) {
	vec3 tMin = (b.min - origin) / dir;
	vec3 tMax = (b.max - origin) / dir;
	vec3 t1 = min(tMin, tMax);
	vec3 t2 = max(tMin, tMax);
	float tNear = max(max(t1.x, t1.y), t1.z);
	float tFar = min(min(t2.x, t2.y), t2.z);
	return vec2(tNear, tFar);
}

bool intersectBoxes(vec3 origin, vec3 dir, out hitinfo info) {
	float smallest = MAX_SCENE_BOUNDS;
	bool found = false;
	for (int i = 0; i < NUM_BOXES; i++) {
		vec2 lambda = intersectBox(origin, dir, boxes[i]);
		if (lambda.x > 0.0 && lambda.x < lambda.y && lambda.x < smallest) {
			info.lambda = lambda;
			info.bi = i;
			smallest = lambda.x;
			found = true;
		}
	}
	return found;
}

bool intersectSpheres(vec3 origin, vec3 dir, out hitinfo info) {
	
	bool found = false;
	for (int i = 0; i < NUM_SPHERES; i++) 
	{
		float t0, t1;

		// geometric solution
        vec3 L = spheres[i].center - origin; 
        float tca = dot(L, dir); 
        if (tca < 0) 
			continue;

        float d2 = dot(L, L) - tca * tca; 

        if (d2 > pow(spheres[i].radius, 2)) 
			continue;

        float thc = sqrt(pow(spheres[i].radius, 2) - d2); 
        t0 = tca - thc; 
        t1 = tca + thc; 

		if(t0 < 0)
		{
			float tmp = t0;
			t0 = t1;
			t1 = tmp;

			if(t0 < 0)
				continue;
		}

		info.lambda = vec2(t0, t1);
		info.bi = i;

		found = true;
	}

	return found;
}

vec4 trace(vec3 origin, vec3 dir) {
	hitinfo info;

	dir = normalize(dir);

	// spheres
	if(intersectSpheres(origin, dir, info)) {
		vec3 hitpoint = origin + dir * info.lambda.x;
		vec3 normal = hitpoint - spheres[info.bi].center;
		normal = normalize(normal);

		vec4 color = dot(normal, -dir) * vec4(1, 0, 0, 1);
		return color;
	}

	// boxes
	if(intersectBoxes(origin, dir, info)) {
		vec3 hitpoint = origin + dir * info.lambda.x;

		// get the normal
		vec3 normal;
		vec3 localPoint = hitpoint - boxes[info.bi].min;
		float min = 9999;
		vec3 extents = boxes[info.bi].max - boxes[info.bi].min;

		float dist = abs(extents.x - localPoint.x);
		if(dist < min)
		{
			min = dist;
			normal = sign(localPoint.x) * vec3(1, 0, 0);
		}

		dist = abs(extents.y - localPoint.y);
		if(dist < min)
		{
			min = dist;
			normal = sign(localPoint.y) * vec3(0, 1, 0);
		}

		dist = abs(extents.z - localPoint.z);
		if(dist < min)
		{
			min = dist;
			normal = sign(localPoint.z) * vec3(0, 0, 1);
		}

		vec4 color = dot(normal, -dir) * vec4(1, 0, 0, 1);
		return color;
	}

	return vec4(0.3, 0.3, 0.3, 1.0);
}

void main(void) {
	ivec2 pix = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(framebuffer);
	if (pix.x >= size.x || pix.y >= size.y) {
		return;
	}

	//ray00 

	vec2 pos = vec2(pix) / vec2(size.x, size.y);
	vec3 dir = mix(mix(ray00, ray01, pos.y), mix(ray10, ray11, pos.y), pos.x);
	vec4 color = trace(eye, dir);
	//color = vec4(spheres[0].center, 1.0f);
	imageStore(framebuffer, pix, color);
}

/*
void main() {
	ivec2 storePos = ivec2(gl_GlobalInvocationID.xy);
	float localCoef = length(vec2(ivec2(gl_LocalInvocationID.xy)-8)/8.0);
	float globalCoef = sin(float(gl_WorkGroupID.x+gl_WorkGroupID.y)*0.1 + time/500)*0.5;

	if(storePos.x < 1024/2)
		imageStore(framebuffer, storePos, vec4(sin(time/1000.0), sin(time/1000.0), 0.3, 1));//vec4(1.0 - globalCoef * localCoef, 0.0, 0.0, 0.0));
	else
		imageStore(framebuffer, storePos, vec4(1.0 - globalCoef * localCoef, 0.0, 0.0, 0.0));
}*/
